
buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    }
}

subprojects {
    group 'eece513'
    version '1.0'

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    configurations {
        distributedTestCompile.extendsFrom testCompile
        distributedTestRuntime.extendsFrom testRuntime
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

project(':distributed-fs') {
    apply plugin: 'com.google.protobuf'

    sourceSets {
        main{
            proto{
                srcDirs += 'distributed-fs/src/main/proto'
            }
        }
        test {
            kotlin {
                srcDirs += "distributed-fs/src/test/unit/kotlin"
            }
        }
    }

    protobuf {
        generatedFilesBaseDir = "$projectDir/src"

        protoc {
            artifact = "com.google.protobuf:protoc:3.5.1"
        }
    }
}

project(':distributed-ls') {
    sourceSets {
        test {
            kotlin {
                srcDirs += "distributed-ls/src/test/unit/kotlin"
            }
        }
        distributedTest {
            kotlin {
                srcDirs += "distributed-ls/src/test/distributed/kotlin"
            }

            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}