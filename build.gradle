
buildscript {
    ext.kotlin_version = '1.2.30'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
    }
}

subprojects {
    group 'eece513'
    version '1.0'

    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

task jar { }
jar.dependsOn ':distributed-fs:jar', ':distributed-ls:jar', ':fs-client:jar'

task configBootstrapAws {
    doLast {
        if (!project.hasProperty("pem")) {
            throw new InvalidUserDataException("must provide path to PEM file!")
        }

        bootstrapAws.commandLine "./scripts/deploymentScript", project.pem, "--all"
    }
}

task bootstrapAws(type: Exec, description: 'Configure AWS servers', group: 'Configuration') {
    dependsOn = ['jar', 'configBootstrapAws']
}
